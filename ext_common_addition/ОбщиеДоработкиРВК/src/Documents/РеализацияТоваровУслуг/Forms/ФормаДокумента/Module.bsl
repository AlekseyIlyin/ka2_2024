#Область ОбработчикиСобытийФормы

&НаСервере
Процедура рвк_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
		
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		рвк_ЗаполнитьНалогообложениеНаСервере(
			Объект.НалогообложениеНДС,
			Объект.Организация,
			Объект.Контрагент);	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура рвк_ПриОткрытииПосле(Отказ)
	рвк_ОбновитьЗадолженностьНаКлиенте();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура рвк_ПартнерПриИзмененииПосле(Элемент)
	рвк_ОбновитьЗадолженностьНаКлиенте();
	
	рвк_ЗаполнитьНалогообложениеНаСервере(
		Объект.НалогообложениеНДС,
		Объект.Организация,
		Объект.Контрагент);	

КонецПроцедуры

&НаКлиенте
Процедура рвк_КонтрагентПриИзмененииПосле(Элемент)
	
	рвк_ЗаполнитьНалогообложениеНаСервере(
		Объект.НалогообложениеНДС,
		Объект.Организация,
		Объект.Контрагент);	

КонецПроцедуры

&НаКлиенте
Процедура рвк_ОрганизацияПриИзмененииПосле(Элемент)

	рвк_ЗаполнитьНалогообложениеНаСервере(
		Объект.НалогообложениеНДС,
		Объект.Организация,
		Объект.Контрагент);	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура рвк_ТоварыНоменклатураАвтоПодборВместо(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ДанныеВыбора 		 = Неопределено;	
	
	НовыйМассив 		 = рвк_НоменклатураВызовСервера.МассивПолученияДанныхВыбор(); 
	ФиксированныйМассив  = Новый ФиксированныйМассив(НовыйМассив);
	
	Отбор 				 = Новый Структура;
	Отбор.Вставить("ТипНоменклатуры",ФиксированныйМассив);
	
	ПараметрыДляПолученияДанныхВыбора = Новый Структура;
	ПараметрыДляПолученияДанныхВыбора.Вставить("НалогообложениеНДС", рвк_НоменклатураВызовСервера.НалогообложениеНДСПолученияДанныхВыбор());
	ПараметрыДляПолученияДанныхВыбора.Вставить("Отбор",Отбор);
	ПараметрыДляПолученияДанныхВыбора.Вставить("СтрокаПоиска",Текст);
	ПараметрыДляПолученияДанныхВыбора.Вставить("ВыборГруппИЭлементов",ИспользованиеГруппИЭлементов.Элементы);
	ПараметрыДляПолученияДанныхВыбора.Вставить("СпособПоискаСтроки",Неопределено);
	ПараметрыДляПолученияДанныхВыбора.Вставить("ПолнотекстовыйПоиск",Неопределено);
	
	рвк_НоменклатураВызовСервера.НоменклатураОбработкаПолученияДанныхВыбораВДокументПродажи(ДанныеВыбора, ПараметрыДляПолученияДанныхВыбора, СтандартнаяОбработка);
	
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура рвк_ЗаполнитьНалогообложениеНаСервере(НалогообложениеНДС, Знач Организация, Знач Контрагент)
	ОрганизацияИНН = рвк_ЗначениеРеквизитаОбъектаНаСервере(Организация, "ИНН");
	КонтрагентНаименование = рвк_ЗначениеРеквизитаОбъектаНаСервере(Контрагент, "Наименование");
	КонтрагентЧастноеЛицо = СтрНачинаетсяС(КонтрагентНаименование, "Частное лицо"); 		
	Если ОрганизацияИНН = "772161119773" И КонтрагентЧастноеЛицо Тогда
		НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД");
	ИначеЕсли ОрганизацияИНН = "772161119773" И НЕ КонтрагентЧастноеЛицо Тогда
		НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС");
	Иначе
		НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС");
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция рвк_ЗначениеРеквизитаОбъектаНаСервере(Объект, ИмяРеквизита)
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект, ИмяРеквизита);
КонецФункции

&НаСервереБезКонтекста
Функция рвк_ПолучитьПросрочкуНаСервере(Клиент)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(РасчетыПоСрокам.ПредоплатаРеглОстаток) КАК НашДолг,
		|	СУММА(РасчетыПоСрокам.ДолгРеглОстаток) КАК Задолженность,
		|	СУММА(ВЫБОР
		|			КОГДА РасчетыПоСрокам.ДатаПлановогоПогашения < НАЧАЛОПЕРИОДА(&ТекущаяДата, ДЕНЬ)
		|				ТОГДА РасчетыПоСрокам.ДолгРеглОстаток
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Просрочено
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки(, АналитикаУчетаПоПартнерам.Партнер = &Партнер) КАК РасчетыПоСрокам";
	
	Запрос.УстановитьПараметр("Партнер", Клиент);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СтруктураРезультата = Новый Структура("Задолженность,Просрочено", 0, 0);
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Задолженность) Тогда
			СтруктураРезультата.Задолженность = ВыборкаДетальныеЗаписи.Задолженность;
		КонецЕсли;
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.Просрочено) Тогда
			СтруктураРезультата.Просрочено = ВыборкаДетальныеЗаписи.Просрочено;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтруктураРезультата;
	
КонецФункции

&НаКлиенте
Процедура рвк_ОбновитьЗадолженностьНаКлиенте()
	
	Если Не ЗначениеЗаполнено(Объект.Партнер) Тогда
		Элементы.ЗадолженностьСтрокой.Заголовок = "";
		Возврат;
	КонецЕсли;
		
	Результат = рвк_ПолучитьПросрочкуНаСервере(Объект.Партнер);
	Если Результат.Задолженность = 0 Тогда
		Элементы.ЗадолженностьСтрокой.Заголовок = "";
	ИначеЕсли Результат.Задолженность = Результат.Просрочено Тогда
		Элементы.ЗадолженностьСтрокой.Заголовок = СтрШаблон("Просрочено: %1", Формат(Результат.Просрочено, "ЧДЦ=0; ЧН="));
	Иначе
		Элементы.ЗадолженностьСтрокой.Заголовок = СтрШаблон("Задолженность: %1 Просрочено: %2", Формат(Результат.Задолженность, "ЧДЦ=0; ЧН="), Формат(Результат.Просрочено, "ЧДЦ=0; ЧН="));
	КонецЕсли;
	
	Если Результат.Просрочено > 0 Тогда
		Элементы.ЗадолженностьСтрокой.ЦветТекста = WebЦвета.КрасноФиолетовый;
	Иначе
		Элементы.ЗадолженностьСтрокой.ЦветТекста = WebЦвета.Черный;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти