#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

&После("ПередЗаписью")
Процедура рвк_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ВключитьИнтеркампани = Константы.рвк_ВключитьИнтеркампани.Получить();	
	ЕстьИнтеркампани = ВключитьИнтеркампани	И рвк_ОбщийМодульПовтИсп.ПартнерНашаОрганизация(Партнер);
	ДополнительныеСвойства.Вставить("ЕстьИнтеркампани", ЕстьИнтеркампани);
	
	Если ЕстьИнтеркампани Тогда
		Дата = НачалоДня(Дата);
	КонецЕсли;
	
КонецПроцедуры

&После("ПриЗаписи")
Процедура рвк_ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	рвк_СформироватьПриобретение(Отказ);
	
КонецПроцедуры


&После("ОбработкаПроведения")
Процедура рвк_ОбработкаПроведения(Отказ, РежимПроведения)
	
	рвк_СформироватьСчетФактуру(Отказ);
	рвк_ПровестиПриобраетение(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаПроведения

Процедура рвк_СформироватьСчетФактуру(Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Если Не рвк_ОбщийМодульПовтИсп.СоздаватьСчетфактурАвтоматически(Организация) Тогда
		Возврат;
	КонецЕсли;

	ПараметрыРегистрации = УчетНДСУПКлиентСервер.ПараметрыРегистрацииСчетовФактурВыданных();
	ЗаполнитьЗначенияСвойств(ПараметрыРегистрации,ЭтотОбъект);
	ПараметрыРегистрации.РеализацияТоваров = Истина;
	РезультатПроверки = УчетНДСУП.СчетаФактурыВыданныеНаОсновании(ПараметрыРегистрации);
	
	ЗначенияРеквизитовКонтрагентаРеализации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, "ИНН,КПП");
	ПерезаполнитьРеквизитыСчетаФактуры = Ложь;
	
	Если ЗначениеЗаполнено(РезультатПроверки.СчетаФактуры) Тогда
		
		ЗначенияРеквизитовСчетаФактуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РезультатПроверки.СчетаФактуры[0], "Номер,Дата,Склад,Контрагент,Партнер,ИННКонтрагента,КППКонтрагента");
		
		ПерезаполнитьРеквизитыСчетаФактуры = 
			ЗначенияРеквизитовСчетаФактуры.Контрагент <> Контрагент ИЛИ 
			ЗначенияРеквизитовСчетаФактуры.Партнер <> Партнер ИЛИ 
			ЗначенияРеквизитовСчетаФактуры.Склад <> Склад ИЛИ 
			ЗначенияРеквизитовКонтрагентаРеализации.ИНН <> ЗначенияРеквизитовСчетаФактуры.ИННКонтрагента ИЛИ 
			ЗначенияРеквизитовКонтрагентаРеализации.КПП <> ЗначенияРеквизитовСчетаФактуры.КППКонтрагента ИЛИ 
			СокрЛП(ЗначенияРеквизитовСчетаФактуры.Номер) <> СокрЛП(Номер) ИЛИ 
			НачалоДня(ЗначенияРеквизитовСчетаФактуры.Дата) <> НачалоДня(Дата);
		
		Если ПерезаполнитьРеквизитыСчетаФактуры Тогда
			СчетФактураОбъект = РезультатПроверки.СчетаФактуры[0].ПолучитьОбъект();
		КонецЕсли;
		
	Иначе
		СчетФактураОбъект = Документы.СчетФактураВыданный.СоздатьДокумент();
		СчетФактураОбъект.Заполнить(Ссылка);
		СчетФактураОбъект.Дата = Дата;
		
		СчетФактураОбъект.Организация = Организация;
		СчетФактураОбъект.ДатаВыставления = Дата;
		
		СчетФактураОбъект.Ответственный = ПараметрыСеанса.ТекущийПользователь;
		СчетФактураОбъект.ДокументОснование = Ссылка;
		СтрокаДокументаОснования = СчетФактураОбъект.ДокументыОснования.Добавить();
		СтрокаДокументаОснования.ХозяйственнаяОперация = ХозяйственнаяОперация;
		СтрокаДокументаОснования.ДокументОснование = Ссылка;
		
		СчетФактураОбъект.ИдентификаторГосКонтракта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ИдентификаторГосКонтракта");
		
		ПерезаполнитьРеквизитыСчетаФактуры = Истина;
	КонецЕсли;
	
	Если ПерезаполнитьРеквизитыСчетаФактуры Тогда
		СчетФактураОбъект.Номер = Номер;
		СчетФактураОбъект.Дата = Дата;
		СчетФактураОбъект.Контрагент = Контрагент;
		СчетФактураОбъект.Партнер = Партнер;
		СчетФактураОбъект.Склад = Склад;
		СчетФактураОбъект.ИННКонтрагента = ЗначенияРеквизитовКонтрагентаРеализации.ИНН;
		СчетФактураОбъект.КППКонтрагента = ЗначенияРеквизитовКонтрагентаРеализации.КПП;
		Попытка
			СчетФактураОбъект.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
		Исключение
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = "Не удалось провести счет-фактуру по причине: " + ОписаниеОшибки();
			СообщениеПользователю.Сообщить();
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

Процедура рвк_ПровестиПриобраетение(Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Приобретение = Неопределено;
	Если ДополнительныеСвойства.Свойство("Приобретение", Приобретение) 
			И ЗначениеЗаполнено(Приобретение) Тогда
		ПриобретениеОбъект = Приобретение.ПолучитьОбъект();
		Попытка
			ПриобретениеОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ТекстСообщения = 
				СтрШаблон("Документ не удалось провести: %1 проверьте заполненение соглашения с поставщиком: %2", Приобретение, ПриобретениеОбъект.Соглашение);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

Процедура рвк_СформироватьПриобретение(Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьИнтеркампани = Ложь;
	Если Не ДополнительныеСвойства.Свойство("ЕстьИнтеркампани", ЕстьИнтеркампани) 
			Или Не ЕстьИнтеркампани Тогда
		Возврат;
	КонецЕсли;
	
	СоглашениеСПоставщиком = рвк_СоглашениеСПоставщиком();
	ПриобретениеGUID = рвк_ПриобретениеИнтеркампани();
	Если ЗначениеЗаполнено(ПриобретениеGUID) Тогда
		Приобретение = Документы.ПриобретениеТоваровУслуг.ПолучитьСсылку(Новый УникальныйИдентификатор(ПриобретениеGUID));
		Если ЗначениеЗаполнено(Приобретение) Тогда
			ПриобретениеОбъект = Приобретение.ПолучитьОбъект();
		КонецЕсли;
		Если ПриобретениеОбъект = Неопределено Тогда
			ПриобретениеОбъект = Документы.ПриобретениеТоваровУслуг.СоздатьДокумент();
		Иначе
			Если ПометкаУдаления <> ПриобретениеОбъект.ПометкаУдаления Тогда
				ПриобретениеОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
			ИначеЕсли ПриобретениеОбъект.Проведен Тогда // Проведение будет в обработчике проведения
				ПриобретениеОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ПометкаУдаления Тогда
			Возврат;
		Иначе
			ПриобретениеОбъект = Документы.ПриобретениеТоваровУслуг.СоздатьДокумент();
		КонецЕсли;
	КонецЕсли;
	
	ПриобретениеОбъект.Заполнить(СоглашениеСПоставщиком);
	ПриобретениеОбъект.Дата = НачалоДня(Дата);
	ПриобретениеОбъект.ДатаВходящегоДокумента = Дата;
	ПриобретениеОбъект.НомерВходящегоДокумента = Номер;
	ПриобретениеОбъект.ЗакупкаПодДеятельность = Перечисления.ТипыНалогообложенияНДС.ПродажаПоПатенту;
	ПриобретениеОбъект.Товары.Загрузить(рвк_ТаблицаТоваровПриобретение());
	ПриобретениеОбъект.Записать();
	
	Приобретение = ПриобретениеОбъект.Ссылка;
	ДополнительныеСвойства.Вставить("Приобретение", Приобретение);
	рвк_ОбновитьСсылкуНаПриобретение(Приобретение);

	ТекстСообщения = СтрШаблон("Сформирован документ: %1", Приобретение);
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
КонецПроцедуры

Функция рвк_ТаблицаТоваровПриобретение()
	ОписаниеТипаНеотрицательнаяСумма = Метаданные.ОпределяемыеТипы.ДенежнаяСуммаНеотрицательная.Тип;
	
	ТаблицаТовары = ВидыЗапасов.Выгрузить();
	ТаблицаТовары.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаТовары.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаТовары.Колонки.Добавить("Сумма", ОписаниеТипаНеотрицательнаяСумма);
	ТаблицаТовары.Колонки.Добавить("СуммаИтог", ОписаниеТипаНеотрицательнаяСумма);
	АналитикаУчетаНоменклатуры = ТаблицаТовары.ВыгрузитьКолонку("АналитикаУчетаНоменклатуры"); 
	РеквизитыАналитикиНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(АналитикаУчетаНоменклатуры, "Номенклатура,Характеристика");
	Для Каждого СтрокаТаблицыТовары Из ТаблицаТовары Цикл
		РеквизитыАналитики = РеквизитыАналитикиНоменклатуры[СтрокаТаблицыТовары.АналитикаУчетаНоменклатуры];
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыТовары, РеквизитыАналитики, "Номенклатура,Характеристика");
		СуммаСНДС = СтрокаТаблицыТовары.СуммаСНДС;
		СтрокаТаблицыТовары.Сумма = СуммаСНДС;
		СтрокаТаблицыТовары.СуммаИтог = СуммаСНДС;
		СтрокаТаблицыТовары.СуммаВзаиморасчетов = СуммаСНДС; 
	КонецЦикла;
	Возврат ТаблицаТовары;	
КонецФункции

Процедура рвк_ОбновитьСсылкуНаПриобретение(Приобретение)
	ТаблицаСвойствИЗначений = УправлениеСвойствами.ЗначенияСвойств(Ссылка, Ложь);
	СвойстваПриобретениеИнтеркампани = рвк_СвойстваПриобретениеИнтеркампани();
	СтрокаЗначенияСвойства = ТаблицаСвойствИЗначений.Найти(СвойстваПриобретениеИнтеркампани, "Свойство");
	Если СтрокаЗначенияСвойства = Неопределено Тогда
		СтрокаЗначенияСвойства = ТаблицаСвойствИЗначений.Добавить();
		СтрокаЗначенияСвойства.Свойство = СвойстваПриобретениеИнтеркампани;
	КонецЕсли;
	СтрокаЗначенияСвойства.Значение = XMLСтрока(Приобретение); 
	УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(Ссылка, ТаблицаСвойствИЗначений); 
КонецПроцедуры

// Приобретение интеркампани.
// 
// Возвращаемое значение:
//  Произвольный - Приобретение интеркампани
Функция рвк_ПриобретениеИнтеркампани()
	Результат = рвк_ОбщегоНазначения.ЗначениеСвойства(Ссылка, рвк_СвойстваПриобретениеИнтеркампани(), Неопределено);
	Возврат Результат;
КонецФункции

Функция рвк_СоглашениеСПоставщиком()
	Перем Результат;
	
	ИмяСвойста = "СоглашениеСПоставщиком_fce5fe05c0de4363ac8f62295fa98d13";
	Гиперссылка = рвк_ОбщегоНазначения.ЗначениеСвойства(Партнер, ИмяСвойста, Неопределено);
	Попытка
		Результат = рвк_ОбщегоНазначения.СсылкуИзНавигационной(Гиперссылка);
	Исключение
		Результат = Неопределено;
	КонецПопытки;
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ТекстСообщения = СтрШаблон("В дополнительных реквизитах Партнера: %1 не указано соглашение с поставщиком", Партнер);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

Функция рвк_СвойстваПриобретениеИнтеркампани()
	ИмяСвойста = "ПриобретениеТоваровИнтеркампани_99632521ff87417294fd4853ca9e1654";
	Результат = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", ИмяСвойста);
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ВызватьИсключение "Не обнаружено доп. свойство " + ИмяСвойста;
	КонецЕсли;
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецЕсли