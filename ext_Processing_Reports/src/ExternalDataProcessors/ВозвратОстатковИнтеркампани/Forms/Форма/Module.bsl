
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СформироватьЗаголовокФормы(); 
	ЗагрузитьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	ЗаписатьНастройкиНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	Направление = Направление * 86400;
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ОбновитьНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОстатки

&НаКлиенте
Процедура ОстаткиКоличествоПриИзменении(Элемент)
	ТекущиеДанные = Элементы.Остатки.ТекущиеДанные;
	Если ТекущиеДанные.Количество > ТекущиеДанные.КоличествоМакс Тогда
		ТекущиеДанные.Количество = ТекущиеДанные.КоличествоМакс;
		ПоказатьПредупреждение(, "Нельзя указывать количество больше чем кол-во по документу или остатка");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыборСнятьВсе(Команда)
	УстановитьПометку(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ВыборВсе(Команда)
	УстановитьПометку(Истина);
КонецПроцедуры

&НаКлиенте
Процедура Обработать(Команда)
	ОбработатьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьЗаголовокФормы()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	СведенияОбработки = ОбработкаОбъект.СведенияОВнешнейОбработке();
	Заголовок = СтрШаблон("%1 v <%2>", СведенияОбработки.Наименование, СведенияОбработки.Версия);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометку(ЗначениеПометки)
	Для Каждого СтрокаОстатки Из Объект.Остатки Цикл
		СтрокаОстатки.Пометка = ЗначениеПометки;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройки()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗагрузитьНастройки();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаписатьНастройки();
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.Обновить();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	Объект.Остатки.Сортировать("Номенклатура,ДокументПередачи");
	Для Каждого СтрокаОстатки Из Объект.Остатки Цикл
		СтрокаОстатки.КоличествоМакс = Мин(СтрокаОстатки.Остаток, СтрокаОстатки.Передано);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ОбработатьНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.Обработать();
	ОбработкаОбъект.Обновить();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
КонецПроцедуры

#КонецОбласти