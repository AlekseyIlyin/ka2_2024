#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СформироватьЗаголовокФормы(); 
	ЗагрузитьНастройки();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого Строка_epsales Из Объект.epsales Цикл
		Если ЭтоПродажа(Строка_epsales.opcode) И Строка_epsales.amount_cp = 0 Тогда
			ПутьКДанным = СтрШаблон("Объект.epsales[%1].amount_cp", Формат(Строка_epsales.НомерСтроки - 1, "ЧН=0; ЧГ=;"));
			ОбщегоНазначения.СообщитьПользователю("Не заполнена сумма покупки/продажи",,, ПутьКДанным, Отказ);
		ИначеЕсли Не ЭтоПродажа(Строка_epsales.opcode) И Строка_epsales.amount_bp = 0 Тогда
			ПутьКДанным = СтрШаблон("Объект.epsales[%1].amount_bp", Формат(Строка_epsales.НомерСтроки - 1, "ЧН=0; ЧГ=;"));
			ОбщегоНазначения.СообщитьПользователю("Не заполнена сумма покупки/продажи",,, ПутьКДанным, Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ДатаНачала) Тогда
		Объект.ДатаОкончания = КонецДня(Объект.ДатаНачала + Объект.ГлубинаОтчетаДней * 86400);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборКаталогаВыгрузкиЗавершение", ЭтотОбъект);
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Показать(ОписаниеОповещения);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьНастройки(Команда)
	ЗаписатьНастройкиНаСервере()
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ПутьВыгрузки) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указан путь выгрузки",,, "Объект.ПутьВыгрузки");
		Возврат;
	КонецЕсли;
	
	АдресРезультатаВыгрузки = ЗаписатьНаСервере();
	ДанныеФайлов = ПолучитьИзВременногоХранилища(АдресРезультатаВыгрузки);
	Для Каждого ИмяДанныеФайла Из ДанныеФайлов Цикл
		ПолноеИмяФайла = СтрШаблон("%1\%2", Объект.ПутьВыгрузки, ИмяДанныеФайла.ИмяФайла);
		ИмяДанныеФайла.ДанныеФайла.Записать(ПолноеИмяФайла);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ПутьКлиентаCisLink) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не выбран клиент CISLINK",,, "Объект.ПутьКлиентаCisLink");
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Объект.ИсходящаяПапкаCisLink) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не выбран клиент CISLINK",,, "Объект.ИсходящаяПапкаCisLink");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(АдресРезультатаВыгрузки) Или Не ЭтоАдресВременногоХранилища(АдресРезультатаВыгрузки) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Данные необходимо выгрузить перед отправкой");
		Возврат;
	КонецЕсли;
	
	ОтправитьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Диалог

&НаСервере
Процедура СформироватьЗаголовокФормы()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	СведенияОбработки = ОбработкаОбъект.СведенияОВнешнейОбработке();
	Заголовок = СтрШаблон("%1 v <%2>", СведенияОбработки.Наименование, СведенияОбработки.Версия);
КонецПроцедуры

&НаКлиенте
Процедура ВыборКаталогаВыгрузкиЗавершение(ВыбранныеИменаФайлов, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ВыбранныеИменаФайлов) Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ПутьВыгрузки = ВыбранныеИменаФайлов[0];
	
КонецПроцедуры

#КонецОбласти

#Область Сервис

&НаСервере
Процедура ЗагрузитьНастройки()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗагрузитьНастройки();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНастройкиНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗаписатьНастройки();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.Заполнить();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
КонецПроцедуры

&НаСервере
Функция ЗаписатьНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	АдресРезультата = ОбработкаОбъект.ЗаписатьДанные(УникальныйИдентификатор);
	Возврат АдресРезультата;
КонецФункции

&НаСервере
Процедура ОтправитьНаСервере()
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.Отправить(АдресРезультатаВыгрузки, Истина);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоПродажа(ТипОперации)
	Возврат ТипОперации = 0 Или ТипОперации = 1 Или ТипОперации = 7;
КонецФункции

#КонецОбласти

#КонецОбласти