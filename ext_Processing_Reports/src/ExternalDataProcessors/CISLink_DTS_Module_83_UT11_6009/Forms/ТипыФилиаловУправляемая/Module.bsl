
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем ШаблонНастроек;
	
	ШаблонНастроек = РеквизитФормыВЗначение("Объект").ШаблонНастроек();
	
	ЭтаФорма.Элементы.ТипыФилиаловФилиал.ОграничениеТипа = ШаблонНастроек.ТипыФилиалов.Колонки.Филиал.ТипЗначения;
	
	Для каждого ТипыФилиаловСтрока Из Параметры.ТипыФилиалов Цикл
	
		ЗаполнитьЗначенияСвойств(ЭтаФорма.ТипыФилиалов.Добавить(), ТипыФилиаловСтрока);
	
	КонецЦикла;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура КомандаТипыФилиаловСнятьФлажки(Команда)
	
	Для каждого ТипыФилиаловСтрока Из ТипыФилиалов.НайтиСтроки(Новый Структура("Используется", Истина)) Цикл
		
		ТипыФилиаловСтрока.Используется = Не ТипыФилиаловСтрока.Используется;
		
	КонецЦикла;
	
КонецПроцедуры // КомандаТипыФилиаловСнятьФлажки()

&НаКлиенте
Процедура КомандаТипыФилиаловУстановитьФлажки(Команда)
	
	Для каждого ТипыФилиаловСтрока Из ТипыФилиалов.НайтиСтроки(Новый Структура("Используется", Ложь)) Цикл
		
		ТипыФилиаловСтрока.Используется = Не ТипыФилиаловСтрока.Используется;
		
	КонецЦикла;
	
КонецПроцедуры // КомандаТипыФилиаловУстановитьФлажки()

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	Результат = Новый Структура("ТипыФилиалов", Новый Массив);
	
	Для каждого ТипыФилиаловСтрока Из ТипыФилиалов Цикл
	
		Результат.ТипыФилиалов.Добавить(Новый Структура(
			"Используется, Филиал, whtype"
			, ТипыФилиаловСтрока.Используется
			, ТипыФилиаловСтрока.Филиал
			, ТипыФилиаловСтрока.whtype
		));
	
	КонецЦикла;
	
	ЭтаФорма.Закрыть(Результат);
	
КонецПроцедуры // КомандаВыбрать()

&НаКлиенте
Процедура ТипыФилиаловПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
	
		ТекущиеДанные = Элементы.ТипыФилиалов.ТекущиеДанные;
		
		ТекущиеДанные.Используется	= Истина;
		ТекущиеДанные.whtype		= 2;
		
	КонецЕсли;
	
КонецПроцедуры // ТипыФилиаловПриНачалеРедактирования()