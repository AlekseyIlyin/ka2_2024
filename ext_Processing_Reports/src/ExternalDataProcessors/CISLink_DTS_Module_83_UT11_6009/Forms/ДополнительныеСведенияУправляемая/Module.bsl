
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ДополнительныеСведенияОбъект.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.Организации");
	
	Для каждого ДополнительныеСведенияСтрокаОписание Из Параметры.ДополнительныеСведения Цикл
		
		ДополнительныеСведенияСтрока = ДополнительныеСведения.Добавить();
		
		ЗаполнитьЗначенияСвойств(ДополнительныеСведенияСтрока, ДополнительныеСведенияСтрокаОписание);
		
	КонецЦикла;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура КомандаОк(Команда)
	
	Закрыть(ДополнительныеСведения());
	
КонецПроцедуры // КомандаОк()

&НаКлиенте
Функция ДополнительныеСведения()

	ЭтаФорма.ЗакрыватьФормуБезПредупреждения = Истина;
	
	Результат = Новый Структура;
	Результат.Вставить("ДополнительныеСведения", Новый Массив);
	
	Для каждого ДополнительныеСведенияСтрока Из ДополнительныеСведения Цикл
	
		ДополнительныеСведенияСтрокаОписание = Новый Структура;
		ДополнительныеСведенияСтрокаОписание.Вставить("Используется"	, ДополнительныеСведенияСтрока.Используется);
		ДополнительныеСведенияСтрокаОписание.Вставить("Объект"			, ДополнительныеСведенияСтрока.Объект);
		ДополнительныеСведенияСтрокаОписание.Вставить("Свойство"		, ДополнительныеСведенияСтрока.Свойство);
		ДополнительныеСведенияСтрокаОписание.Вставить("Значение"		, ДополнительныеСведенияСтрока.Значение);
		
		Результат.ДополнительныеСведения.Добавить(ДополнительныеСведенияСтрокаОписание);
	
	КонецЦикла;
	
	Возврат Результат;

КонецФункции // ДополнительныеСведения()

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не Модифицированность или ЗавершениеРаботы или ЭтаФорма.ЗакрыватьФормуБезПредупреждения Тогда
	
		Возврат;
	
	КонецЕсли;
	
	Отказ = Истина;
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтаФорма)
		, "Сохранить изменения?"
		, РежимДиалогаВопрос.ДаНетОтмена
	);
	
КонецПроцедуры // ПередЗакрытием()

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		
		ЭтаФорма.ЗакрыватьФормуБезПредупреждения = Истина;
		Закрыть();
		
		Возврат;
		
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ЭтаФорма.ЗакрыватьФормуБезПредупреждения = Истина;
		Закрыть(ДополнительныеСведения());
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗакрытиемЗавершение()

&НаКлиенте
Процедура ДополнительныеСведенияПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры // ДополнительныеСведенияПриИзменении()

&НаКлиенте
Процедура ДополнительныеСведенияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Перем ТекущиеДанные;
	
	Если Не НоваяСтрока Тогда
	
		Возврат;
	
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ДополнительныеСведения.ТекущиеДанные;
	ТекущиеДанные.Используется = Истина;
	
КонецПроцедуры // ДополнительныеСведенияПриНачалеРедактирования()

&НаКлиенте
Процедура КомандаУстановитьФлажки(Команда)
	
	Для каждого ДополнительныеСведенияСтрока Из ДополнительныеСведения Цикл
	
		ДополнительныеСведенияСтрока.Используется = Истина;
	
	КонецЦикла;
	
КонецПроцедуры // КомандаУстановитьФлажки()

&НаКлиенте
Процедура КомандаСнятьФлажки(Команда)
	
	Для каждого ДополнительныеСведенияСтрока Из ДополнительныеСведения Цикл
	
		ДополнительныеСведенияСтрока.Используется = Ложь;
	
	КонецЦикла;
	
КонецПроцедуры // КомандаСнятьФлажки()