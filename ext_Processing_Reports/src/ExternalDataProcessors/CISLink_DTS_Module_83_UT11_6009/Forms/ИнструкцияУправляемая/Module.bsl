
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем Инструкция;
	
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("ИнструкцияУправляемая");
	
	Инструкция = Макет.ПолучитьТекст();
	
	НомПоз1 = 0;
	
	МассивID = Новый Массив;
	
	Сигнатура1 = "<a href=""#";	Сигнатура1Длина = СтрДлина(Сигнатура1);
	Сигнатура2 = """>";			Сигнатура2Длина = СтрДлина(Сигнатура2);
	
	Пока Истина Цикл
	
		НомПоз1 = СтрНайти(Инструкция, Сигнатура1, , НомПоз1 + 1);
		Если Не НомПоз1 Тогда
		
			Прервать;
		
		КонецЕсли;
		
		НомПоз2 = СтрНайти(Инструкция, Сигнатура2, , НомПоз1 + Сигнатура1Длина);
		Если Не НомПоз2 Тогда
		
			Прервать;
		
		КонецЕсли;
		
		ID = Сред(Инструкция, НомПоз1 + Сигнатура1Длина, НомПоз2 - НомПоз1 - Сигнатура1Длина);
		
		Если СтрНайти(Инструкция, "id=""" + СокрЛП(ID) + """") Тогда
		
			Если МассивID.Найти(ID) = Неопределено Тогда
			
				МассивID.Добавить(ID);
			
			КонецЕсли;
		
		КонецЕсли;
	
	КонецЦикла;
	
	Для каждого ID Из МассивID Цикл
		
		НовыйID = "ID_" + СтрЗаменить(Новый УникальныйИдентификатор, "-", "");
		
		Инструкция = СтрЗаменить(Инструкция, Сигнатура1 + ID + Сигнатура2, Сигнатура1 + НовыйID + Сигнатура2);
		Инструкция = СтрЗаменить(Инструкция, "id=""" + СокрЛП(ID) + """", "id=""" + НовыйID + """");
	
	КонецЦикла;
	
	ЭтаФорма.Инструкция = Инструкция;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура КомандаНазад(Команда)
	Элементы.Инструкция.Назад();
КонецПроцедуры // КомандаНазад()

&НаКлиенте
Процедура КомандаВперед(Команда)
	Элементы.Инструкция.Вперед();
КонецПроцедуры // КомандаВперед()