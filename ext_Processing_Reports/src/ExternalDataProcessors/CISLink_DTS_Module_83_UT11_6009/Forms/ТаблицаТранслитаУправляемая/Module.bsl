
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для каждого ТаблицаТранслитаСтрокаОписание Из Параметры.ТаблицаТранслита Цикл
		
		ТаблицаТранслитаСтрока = ТаблицаТранслита.Добавить();
		
		ЗаполнитьЗначенияСвойств(ТаблицаТранслитаСтрока, ТаблицаТранслитаСтрокаОписание);
		
	КонецЦикла;
	
	ТаблицаТранслитаУстановитьЗаголовок();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не Модифицированность или ЗавершениеРаботы или ЭтаФорма.ЗакрыватьФормуБезПредупреждения Тогда
	
		Возврат;
	
	КонецЕсли;
	
	Отказ = Истина;
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтаФорма)
		, "Сохранить изменения?"
		, РежимДиалогаВопрос.ДаНетОтмена
	);
	
КонецПроцедуры // ПередЗакрытием()

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		
		ЭтаФорма.ЗакрыватьФормуБезПредупреждения = Истина;
		Закрыть();
		
		Возврат;
		
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ЭтаФорма.ЗакрыватьФормуБезПредупреждения = Истина;
		Закрыть(ТаблицаТранслита());
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗакрытиемЗавершение()

&НаКлиенте
Функция ТаблицаТранслита()

	Результат = Новый Структура;
	Результат.Вставить("ТаблицаТранслита", Новый Массив);
	
	Для каждого ТаблицаТранслитаСтрока Из ТаблицаТранслита Цикл
	
		ТаблицаТранслитаСтрокаОписание = Новый Структура;
		ТаблицаТранслитаСтрокаОписание.Вставить("БукваАлфавита"			, ТаблицаТранслитаСтрока.БукваАлфавита);
		ТаблицаТранслитаСтрокаОписание.Вставить("БукваАлфавитаТранслит"	, ТаблицаТранслитаСтрока.БукваАлфавитаТранслит);
		
		Результат.ТаблицаТранслита.Добавить(ТаблицаТранслитаСтрокаОписание);
	
	КонецЦикла;
	
	Возврат Результат;

КонецФункции // ТаблицаПересчета()

&НаКлиенте
Процедура КомандаОк(Команда)
	
	ЭтаФорма.ЗакрыватьФормуБезПредупреждения = Истина;
	Закрыть(ТаблицаТранслита());
	
КонецПроцедуры // КомандаОк()

&НаКлиенте
Процедура КомандаЗаполнить(Команда)
	
	Если Не ЗначениеЗаполнено(ЗаполнитьОтА) или Не ЗначениеЗаполнено(ЗаполнитьДоЯ) Тогда
	
		Возврат;
	
	КонецЕсли;
	
	Для х = КодСимвола(ВРег(ЗаполнитьОтА)) По КодСимвола(ВРег(ЗаполнитьДоЯ)) Цикл
	
		БукваАлфавита = Символ(х);
		
		Если НРег(БукваАлфавита) = БукваАлфавита Тогда // Не буква (регистронезависимая)
		
			Продолжить;
		
		КонецЕсли;
		
		Если Не ТаблицаТранслита.НайтиСтроки(Новый Структура("БукваАлфавита", БукваАлфавита)).Количество() Тогда
		
			ТаблицаТранслита.Добавить().БукваАлфавита = БукваАлфавита;
		
		КонецЕсли;
	
	КонецЦикла;
	
	Модифицированность = Истина;
	
	ТаблицаТранслитаУстановитьЗаголовок();
	
КонецПроцедуры // КомандаЗаполнить()

&НаКлиенте
Процедура ТаблицаТранслитаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	ТаблицаТранслитаУстановитьЗаголовок();
	
КонецПроцедуры // ТаблицаТранслитаПриИзменении()

Процедура ТаблицаТранслитаУстановитьЗаголовок()

	Элементы.ТаблицаТранслита.Заголовок = "Алфавит " + ТаблицаТранслита.Количество();

КонецПроцедуры // ТаблицаТранслитаУстановитьЗаголовок()