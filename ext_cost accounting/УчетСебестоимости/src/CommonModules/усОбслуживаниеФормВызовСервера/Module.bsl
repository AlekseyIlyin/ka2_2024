#Область СлужебныйПрограммныйИнтерфейс

Функция ОстаткиПартийПоНоменклатуре(Знач Дата, Знач Склад, Знач СписокНоменклатуры) Экспорт

	ЭтоСегодня = НачалоДня(Дата) = НачалоДня(ТекущаяДатаСеанса()); 
	Если Не ЗначениеЗаполнено(Дата) Или ЭтоСегодня Тогда
		Дата = Неопределено;
	КонецЕсли;
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Номенклатура,
		|	&Склад
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка В (&СписокНоменклатуры)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КлючиАналитикиУчетаНоменклатуры.Ссылка КАК АналитикаУчетаНоменклатуры,
		|	ВТ_Товары.Номенклатура
		|ПОМЕСТИТЬ ВТ_АналитикаНоменклатуры
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
		|		ПО ВТ_Товары.Номенклатура = КлючиАналитикиУчетаНоменклатуры.Номенклатура
		|		И ВТ_Товары.Склад = КлючиАналитикиУчетаНоменклатуры.МестоХранения
		|СГРУППИРОВАТЬ ПО
		|	КлючиАналитикиУчетаНоменклатуры.Ссылка,
		|	ВТ_Товары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	усОстаткиПоПартиямОстатки.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	усОстаткиПоПартиямОстатки.Партия КАК Партия,
		|	усОстаткиПоПартиямОстатки.КоличествоОстаток,
		|	усОстаткиПоПартиямОстатки.СтоимостьОстаток
		|ИЗ
		|	РегистрНакопления.усОстаткиПоПартиям.Остатки(&Граница, АналитикаУчетаНоменклатуры В
		|		(ВЫБРАТЬ
		|			ВТ_АналитикаНоменклатуры.АналитикаУчетаНоменклатуры
		|		ИЗ
		|			ВТ_АналитикаНоменклатуры КАК ВТ_АналитикаНоменклатуры)) КАК усОстаткиПоПартиямОстатки
		|ГДЕ
		|	усОстаткиПоПартиямОстатки.КоличествоОстаток > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Партия
		|ИТОГИ
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	Запрос.УстановитьПараметр("Граница", Дата);
	
	ВыборкаПоНоменклатуре = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоНоменклатуре.Следующий() Цикл
		
		СведенияПоПартиям = Новый Массив;
		
		ВыборкаПоПартиям = ВыборкаПоНоменклатуре.Выбрать();
		Пока ВыборкаПоПартиям.Следующий() Цикл
			СтрокаПартии = Новый Структура("Партия,КоличествоОстаток,СтоимостьОстаток");
			ЗаполнитьЗначенияСвойств(СтрокаПартии, ВыборкаПоПартиям);
			
			Если ЗначениеЗаполнено(ВыборкаПоПартиям.Партия) Тогда
				ПредставлениеПартии = Формат(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаПоПартиям.Партия, "Дата"), "ДЛФ=D;");
			Иначе
				ПредставлениеПартии = "----------";
			КонецЕсли;
			
			Если ВыборкаПоПартиям.КоличествоОстаток <> 0 И ВыборкаПоПартиям.СтоимостьОстаток <> 0 Тогда
				ПредставлениеСебестоимости = Формат(ВыборкаПоПартиям.СтоимостьОстаток / ВыборкаПоПартиям.КоличествоОстаток, "ЧДЦ=2; ЧГ=;");
			Иначе
				ПредставлениеСебестоимости = "0.00"
			КонецЕсли;
			
			СтрокаПартии.Партия = СтрШаблон("%1 : %2", ПредставлениеПартии, ПредставлениеСебестоимости);
			СведенияПоПартиям.Добавить(СтрокаПартии);
		КонецЦикла;
		
		Результат.Вставить(ВыборкаПоНоменклатуре.Номенклатура, СведенияПоПартиям);
		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

#КонецОбласти
