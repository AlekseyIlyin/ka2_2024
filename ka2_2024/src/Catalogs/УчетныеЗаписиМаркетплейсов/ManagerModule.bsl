
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает хеш настроек для переданной структуры настроек.
//
// Параметры:
//  СтруктураНастроек - Структура - структура с ключами настроек, см. СтрокаИменНастроекУчетнойЗаписи().
// 
// Возвращаемое значение:
//  ХешСумма - строковое представление хеш суммы.
//
Функция ПолучитьХешНастроек(СтруктураНастроек) Экспорт
	
	Возврат ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьХешСтроки(ЗначениеВСтрокуВнутр(СтруктураНастроек));
	
КонецФункции

// Возвращает хеш настроек для учетной записи.
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись, для которой необходимо вернуть хеш настроек.
// 
// Возвращаемое значение:
//  ХешСумма - строковое представление хеш суммы.
//
Функция ПолучитьХешНастроекПоСсылке(УчетнаяЗапись) Экспорт 
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда 
		Возврат Неопределено;
	КонецЕсли;	
	
	СтруктураНастроек = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УчетнаяЗапись, СтрокаИменНастроекУчетнойЗаписи());
	
	СтруктураНастроек.Вставить("ВидыЦен", СтруктураНастроек.ВидыЦен.Выгрузить());
	
	Возврат ПолучитьХешНастроек(СтруктураНастроек);
	
КонецФункции

// Возвращает доступную учетную запись в случае, если она является единственной в справочнике.
//
//  В случае отсутствия записей или наличия более одной возвращается пустая ссылка.
//
// Параметры:
//  ВидМаркетплейса	 - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса для получения учетной записи.
//
// Возвращаемое значение:
//  СправочникСсылка.НастройкиИнтеграцииСМаркетплейсом - Учетная запись по-умолчанию.
//
Функция ОсновнаяУчетнаяЗапись(ВидМаркетплейса) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	УчетныеЗаписиМаркетплейсов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
	|ГДЕ
	|	УчетныеЗаписиМаркетплейсов.ВидМаркетплейса = &ВидМаркетплейса
	|	И УчетныеЗаписиМаркетплейсов.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("ВидМаркетплейса", ВидМаркетплейса);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат ПредопределенноеЗначение("Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка");
	КонецЕсли;

	Выборка = Результат.Выбрать();
	Если Выборка.Количество() = 2 Тогда
		Возврат ПредопределенноеЗначение("Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка");
	КонецЕсли;

	Выборка.Следующий();
	Возврат Выборка.Ссылка;

КонецФункции

// Возвращает структуру с настройками учетной записи.
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись для получения настроек.
// 
// Возвращаемое значение:
//  Структура - Настройки учетной записи, см. Справочники.УчетныеЗаписиМаркетплейсов.СтрокаИменНастроекУчетнойЗаписи()
//
Функция НастройкиУчетнойЗаписи(УчетнаяЗапись) Экспорт

	ЗначенияНастроек = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УчетнаяЗапись, СтрокаИменНастроекУчетнойЗаписи());

	ВидыЦен = Новый Структура;
	ОписаниеВидовЦен = ОписаниеВидовЦенУчетнойЗаписи(ЗначенияНастроек.ВидМаркетплейса);
	
	Для Каждого ОписаниеВидаЦен Из ОписаниеВидовЦен Цикл
		ВидыЦен.Вставить(ОписаниеВидаЦен.Ключ, ПредопределенноеЗначение("Справочник.ВидыЦен.ПустаяСсылка"));
	КонецЦикла;

	Если ЗначенияНастроек.ВидыЦен <> Неопределено Тогда
		Выборка = ЗначенияНастроек.ВидыЦен.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ВидыЦен.Свойство(Выборка.ИмяНастройки) Тогда
				ВидыЦен[Выборка.ИмяНастройки] = Выборка.ВидЦены;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	ЗначенияНастроек.ВидыЦен = ВидыЦен;

	Возврат ЗначенияНастроек;

КонецФункции

// Возвращает информацию по видам цен учетной записи.
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись, для которой необходимо вернуть хеш настроек.
// 
// Возвращаемое значение:
//  Структура - ключом является идентификатор вида цены на маркетплейсе, значением информация по виду цены.
//
Функция ВидыЦенУчетнойЗаписи(УчетнаяЗапись) Экспорт

	ВидыЦенУчетнойЗаписи = Новый Структура;
	
	НастройкиУчетнойЗаписи = НастройкиУчетнойЗаписи(УчетнаяЗапись);
	ВидыЦен = НастройкиУчетнойЗаписи.ВидыЦен;
	ОписаниеВидовЦен = ОписаниеВидовЦенУчетнойЗаписи(НастройкиУчетнойЗаписи.ВидМаркетплейса);

	Для Каждого ДанныеВидаЦен Из ВидыЦен Цикл
		ОписаниеВидаЦен = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеВидовЦен, ДанныеВидаЦен.Ключ, Неопределено);
		ТребуетсяПроверкаЗаполнения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеВидаЦен, "ПроверкаЗаполнения", Ложь);
		ПредставлениеВидаЦены = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеВидаЦен, "ПредставлениеВидаЦены", "");

		СтруктураДанныхВидаЦен = Новый Структура;
		СтруктураДанныхВидаЦен.Вставить("ВидЦены", ДанныеВидаЦен.Значение);
		СтруктураДанныхВидаЦен.Вставить("ПроверкаЗаполнения", ТребуетсяПроверкаЗаполнения);
		СтруктураДанныхВидаЦен.Вставить("ПредставлениеВидаЦены", ПредставлениеВидаЦены);

		ВидыЦенУчетнойЗаписи.Вставить(ДанныеВидаЦен.Ключ, СтруктураДанныхВидаЦен);
	КонецЦикла;

	Возврат ВидыЦенУчетнойЗаписи;

КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает структуру с именами видов цен и признаком проверки заполнения цены.
//
// Возвращаемое значение:
//  Структура, где ключ - имя настройки с видом цен, значение - структура с ключами:
//								ПредставлениеВидаЦены - Строка - представление вида цены в форме настроек синхронизации,
//								ПроверкаЗаполнения - Булево - признак необходимости проверки заполнения цены.
//
Функция ОписаниеВидовЦенУчетнойЗаписи(ВидМаркетплейса)

	ОписаниеВидовЦен = Новый Структура;
	
	Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon")
		Или Не ЗначениеЗаполнено(ВидМаркетплейса) Тогда

		ТипыЦен = ИнтеграцияСМаркетплейсамиСерверЛокализация.ТипыЦенOzon(Истина, Ложь);
		Для каждого ТипЦен Из ТипыЦен Цикл
			ДанныеВидаЦены = Новый Структура("ПредставлениеВидаЦены, ПроверкаЗаполнения",
					ТипЦен.Наименование, ТипЦен.ОбязательноеЗаполнение);
			ОписаниеВидовЦен.Вставить(ТипЦен.Идентификатор, ДанныеВидаЦены);
		КонецЦикла;
		
	КонецЕсли;

	Возврат ОписаниеВидовЦен;

КонецФункции

Функция СтрокаИменНастроекУчетнойЗаписи()
	
	СтрокаИменНастроекУчетнойЗаписи =
			"ВидМаркетплейса, Организация, ИдентификаторКлиента, ИсточникКатегории, ВалютаУчета, ВидыЦен";
	Возврат СтрокаИменНастроекУчетнойЗаписи;
	
КонецФункции

// Возвращает виды цен учетных записей маркетплейса Ozon.
// 
// Возвращаемое значение:
//  Массив - массив видов цен учетных записей маркетплейса Ozon.
//
Функция ВидыЦенУчетныхЗаписейМаркетплейсаOzon() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВидыЦенУчетныхЗаписей.ВидЦены КАК ВидЦены
	|ИЗ
	|	Справочник.УчетныеЗаписиМаркетплейсов.ВидыЦен КАК ВидыЦенУчетныхЗаписей
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
	|		ПО ВидыЦенУчетныхЗаписей.Ссылка = УчетныеЗаписиМаркетплейсов.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК СправочникВидыЦен
	|		ПО (СправочникВидыЦен.Ссылка = ВидыЦенУчетныхЗаписей.ВидЦены)
	|ГДЕ
	|	НЕ УчетныеЗаписиМаркетплейсов.ПометкаУдаления
	|	И УчетныеЗаписиМаркетплейсов.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.МаркетплейсOzon)
	|	И НЕ ВидыЦенУчетныхЗаписей.ВидЦены = ЗНАЧЕНИЕ(Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка)
	|	И НЕ СправочникВидыЦен.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыЦен.Ссылка
	|ИЗ
	|	Справочник.ВидыЦен КАК ВидыЦен
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УчетныеЗаписиМаркетплейсов.ВидыЦен КАК ВидыЦенУчетныхЗаписей
	|		ПО ВидыЦен.Ссылка = ВидыЦенУчетныхЗаписей.ВидЦены
	|ГДЕ
	|	НЕ ВидыЦен.ПометкаУдаления
	|	И ВидыЦен.СпособЗаданияЦены = ЗНАЧЕНИЕ(Перечисление.СпособыЗаданияЦен.ЗагружаетсяСOzon)
	|	И ВидыЦенУчетныхЗаписей.ВидЦены ЕСТЬ NULL";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Новый Массив;
	КонецЕсли;

	Выгрузка = Результат.Выгрузить();
	Возврат Выгрузка.ВыгрузитьКолонку("ВидЦены");

КонецФункции

// Возвращает количество подключений к площадке маркетплейса.
//
// Параметры:
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид маркетплейса.
// 
// Возвращаемое значение:
//  Число - количество подключений.
//
Функция ПолучитьКоличествоПодключений(ВидМаркетплейса) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидМаркетплейса", ВидМаркетплейса);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(КОЛИЧЕСТВО(РАЗЛИЧНЫЕ УчетныеЗаписиМаркетплейсов.Ссылка), 0) КАК КоличествоПодключений
	|ИЗ
	|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
	|ГДЕ
	|	НЕ УчетныеЗаписиМаркетплейсов.ПометкаУдаления
	|	И УчетныеЗаписиМаркетплейсов.ВидМаркетплейса = &ВидМаркетплейса";

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();

	Возврат ВыборкаДетальныеЗаписи.КоличествоПодключений;

КонецФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик                 = Обработчики.Добавить();
	Обработчик.Процедура       = "Справочники.УчетныеЗаписиМаркетплейсов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.12.146";   
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("66a9d101-f7d6-4ac0-a584-4dde0519d84a");  
	Обработчик.РежимВыполнения = "Отложенно";  
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.УчетныеЗаписиМаркетплейсов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";   
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий     = НСтр("ru = 'Получает из сервиса Яндекс Маркет идентификатор кабинета и сохраняет в соответствующий элемент справочника ""Учетные записи маркетплейсов"".'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Справочники.УчетныеЗаписиМаркетплейсов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");

	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.УчетныеЗаписиМаркетплейсов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");

КонецПроцедуры  

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Владелец = "ЯндексМаркетВитринаПлюсФулфилмент";
	ДанныеАвторизации = "organization_id";
	
	Организация =
	ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Владелец, ДанныеАвторизации, Ложь);
	
	Если ЗначениеЗаполнено(Организация) И ТипЗнч(Организация) = Тип("СправочникСсылка.Организации") Тогда 
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры,Организация);  
	КонецЕсли;
		
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
		
	ПолноеИмяОбъекта = "Справочник.Организации";
	Если Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта) Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;   
	
	Отказ  = Ложь;
	ТекстОшибки = "";
	
	СсылкиДляОбработки =  ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	Если СсылкиДляОбработки.Следующий() Тогда
		Организация = СсылкиДляОбработки.Ссылка;
	КонецЕсли;
	
	Если Организация<>Неопределено Тогда
		
		ИдентификаторОрганизации = Строка(Организация.УникальныйИдентификатор());
		Владелец = ИдентификаторОрганизации + "/ЯндексМаркетВитринаПлюсФулфилмент";
		ДанныеАвторизации = "access_token, access_token_expires, refresh_token, campaign_id";
		
		ТекущиеДанные =
		ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Владелец, ДанныеАвторизации, Ложь);
		
	КонецЕсли;
	
	Если ТекущиеДанные<>Неопределено Тогда
		
		ИдентификаторКампании = ТекущиеДанные.campaign_id;
		
		Запрос = Новый Запрос();
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	УчетныеЗаписиМаркетплейсов.Ссылка КАК УчетнаяЗапись
		|ИЗ
		|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
		|ГДЕ
		|	УчетныеЗаписиМаркетплейсов.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет)
		|	И НЕ УчетныеЗаписиМаркетплейсов.ПометкаУдаления";
		Результат = Запрос.Выполнить().Выбрать();
		
		Если  Результат.Следующий() Тогда
			УчетнаяЗаписьПоУмолчанию  = Результат.УчетнаяЗапись.ПолучитьОбъект();
		КонецЕсли;		  
		
		Попытка      
						
			СтруктураОтвета = ИнтеграцияСЯндексМаркетСервер.ПолучитьМагазиныПользователяИзСервиса(ТекущиеДанные, Отказ);
			Магазины = СтруктураОтвета.Результат;
			ТекстОшибки = СтруктураОтвета.ТекстОшибки;

			Если НЕ Отказ Тогда		
				Для Каждого ЭлементКоллекции Из Магазины.campaigns Цикл   
					Если ИдентификаторКампании = Формат(ЭлементКоллекции.id, "ЧН=; ЧГ=0") Тогда    
						Если УчетнаяЗаписьПоУмолчанию <> Неопределено Тогда
							УчетнаяЗаписьПоУмолчанию.ИдентификаторКабинета = Формат(ЭлементКоллекции.business.id, "ЧН=; ЧГ=0");
							УчетнаяЗаписьПоУмолчанию.Записать();
						КонецЕсли;
					КонецЕсли;	
				КонецЦикла;
			КонецЕсли;  
					
		Исключение  
			Отказ  = Истина;
			ТекстОписанияСобытия = НСтр("ru = 'Ошибка сохранения данных для элемента справочника %1.'");  
			ТекстОписанияСобытия = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОписанияСобытия, Метаданные.Справочники.УчетныеЗаписиМаркетплейсов.Представление());
			ЗаписьЖурналаРегистрации(ТекстОписанияСобытия,
			УровеньЖурналаРегистрации.Ошибка,,,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЕсли;  

	Если НЕ Отказ Тогда
		ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Организация);
	Иначе
		ТекстСообщения = НСтр("ru = 'Ошибка обработчика обновления данных справочника %1.'");  
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Метаданные.Справочники.УчетныеЗаписиМаркетплейсов.Представление());
		Если ТекстОшибки<>"" Тогда
			ТекстСообщения = ТекстСообщения + ТекстОшибки;
		КонецЕсли;
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Ошибка,
		Организация.Метаданные(),
		Организация,
		ТекстСообщения);
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли


